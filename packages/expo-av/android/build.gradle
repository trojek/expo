buildscript {
  repositories {
    google()
    jcenter()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:3.1.3'
  }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'

group = 'host.exp.exponent'
version = '1.1.0'

// Upload android library to maven with javadoc and android sources
configurations {
  deployerJars
}

// Creating sources with comments
task androidSourcesJar(type: Jar) {
  classifier = 'sources'
  from android.sourceSets.main.java.srcDirs
}

// Put the androidSources and javadoc to the artifacts
artifacts {
  archives androidSourcesJar
}

uploadArchives {
  repositories {
    mavenDeployer {
      configuration = configurations.deployerJars
      repository(url: mavenLocal().url)
    }
  }
}

android {
  compileSdkVersion 26

  defaultConfig {
    minSdkVersion 21
    targetSdkVersion 26
    versionCode 7
    versionName "1.1.0"
  }
  lintOptions {
    abortOnError false
  }
}

if (new File(rootProject.projectDir.parentFile, 'pubspec.yaml').exists()) {
  apply from: project(":expo_core").file("../expo-core.gradle")
} else if (new File(rootProject.projectDir.parentFile, 'package.json').exists()) {
  apply from: project(":expo-core").file("../expo-core.gradle")
} else {
  throw new GradleException(
      "'expo-core.gradle' was not found in the usual Flutter or React Native dependency locations. " +
          "This package can only be used in such projects. Are you sure you've installed the dependencies properly?")
}

dependencies {
  expendency "expo-core"
  expendency "expo-permissions-interface"
  // Newer version introduces dependency versions conflict
  // on 'com.android.support:support-annotations'
  api 'com.google.android.exoplayer:exoplayer:2.6.1'

  // This dependency allows us to implement CustomHeadersOkHttpDataSourceFactory
  // We depend on it as `provided`, because in Expo apps we use an `expolib_v1`-prefixed
  // version of `okhttp`, so we head to prefix this extension too (available in `expokit`
  // as `expolib_v1.com.google.android.exoplayer:expolib_v1-extension-okhttp:2.6.1`.
  // In plain apps developer will need to add an explicit dependency on this package
  // to `app/build.gradle`, in Expo apps `expoview` depends on `expolib_v1` dependency
  // overridding CustomHeadersOkHttpDataSourceFactory, so the code that uses unprefixed
  // extension won't ever be executed.
  provided 'com.google.android.exoplayer:extension-okhttp:2.6.1'

  api 'com.yqritc:android-scalablevideoview:1.0.1'

  api "com.squareup.okhttp3:okhttp:3.10.0"
  api "com.squareup.okhttp3:okhttp-urlconnection:3.10.0"
}
